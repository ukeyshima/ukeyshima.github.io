<!DOCTYPE html><html lamg="ja"><head><script id="fs" type="x-shader/x-fragment">precision mediump float;
    uniform float time;
    uniform vec2  resolution;
    
    float random(vec2 p){
        return fract(sin(dot(p.xy,vec2(12.9898,78.233)))*43758.5453123)*2.0-1.0;
    }
    float boxDistFunc(vec3 p,vec3 b,vec3 c){
        return length(max(abs(p-c)-b,0.0));
    }
    float floorDistFunc(vec3 p){
        vec3 q=p;
        q.xz=mod(p.xz,1.0)-0.5;
        return boxDistFunc(q,vec3(0.4,abs(random(floor(p.xz)))+abs(2.0*floor(p.z/2.0)*sin(time*1.3*random(floor(p.xz)))),0.4),vec3(0.0,0.0,0.0));
    }
    float distFunc(vec3 p){
        return floorDistFunc(p);
    }
    vec3 genNormal(vec3 p){
        float d=0.001;
        return normalize(vec3(
            distFunc(p+vec3(d,0.0,0.0))-distFunc(p+vec3(-d,0.0,0.0)),
            distFunc(p+vec3(0.0,d,0.0))-distFunc(p+vec3(0.0,-d,0.0)),
            distFunc(p+vec3(0.0,0.0,d))-distFunc(p+vec3(0.0,0.0,-d))
            ));
    }
    vec3 getRayColor(inout vec3 ray,inout vec3 origin,out bool hit){
        float distance=0.0;
        float rLen=0.0;
        vec3 rPos=origin;
        vec3 color=vec3(1.0);
        float maxDist=30.0;
        float marchCount=0.0;
        for(int i=0;i<100;i++){
            distance=distFunc(rPos);
            if(abs(distance)<0.01){
                if(distance==floorDistFunc(rPos)){
                    color=vec3(0.7,0.3,0.7);
                }
                break;
            }
            float t1=(1.0-fract(rPos.z))/ray.z;
            vec3 d1=rPos+t1*ray;
            float t2=(1.0-fract(rPos.x))/ray.x;
            vec3 d2=rPos+t2*ray;
            float t3=(-fract(rPos.x))/ray.x;
            vec3 d3=rPos+t3*ray;
            float t4=(-fract(rPos.z))/ray.z;
            vec3 d4=rPos+t4*ray;
            if(ray.z>0.0 && d1.x>=floor(rPos.x) && d1.x<=floor(rPos.x)+1.0 && abs(distance)>=length(t1*ray)){
                rLen+=length(t1*ray)+0.01;
            }else if(ray.x>0.0 && d2.z>=floor(rPos.z) && d2.z<=floor(rPos.z)+1.0 && abs(distance)>=length(t2*ray)){
                rLen+=length(t2*ray)+0.01;
            }else if(ray.x<0.0 && d3.z>=floor(rPos.z) && d3.z<=floor(rPos.z)+1.0 && abs(distance)>=length(t3*ray)){
                rLen+=length(t3*ray)+0.01;
            }else if(ray.z<0.0 && d4.x>=floor(rPos.x) && d4.x<=floor(rPos.x)+1.0 && abs(distance)>=length(t4*ray)){
                rLen+=length(t4*ray)+0.01;
            }else{
                rLen+=distance;
            }
            rPos=origin+rLen*ray;
            if(rLen>maxDist){
                break;
            }
            marchCount++;
        }
        hit=false;
    if(abs(distance)<0.01){
        color+=marchCount/100.0/pow(rLen,2.0);
        vec3 normal=genNormal(rPos);
        origin=rPos+normal*0.02;
        ray=normalize(reflect(ray,normal));
        hit=true;
    }
        return color;
    }
    vec3 pixelColor(vec2 p){
        vec3 cPos=vec3(0.0,1.5+time,time);
        vec3 cDir=vec3(0.0,0.0,1.0);
        vec3 cUp=vec3(0.0,1.0,0.0);
        vec3 cSide=cross(cDir,cUp);
        float depth=1.0;
        vec3 ray=normalize(cSide*p.x+cUp*p.y+cDir*depth);
        vec3 origin=cPos;
        vec3 color=vec3(0.0);
        float alpha=242.0/255.0;
        bool hit;
        for(int i=0;i<3;i++){
            color+=alpha*getRayColor(ray,origin,hit);
            alpha*=0.3;
            if(!hit)break;
        }
       return color;
    }
    
    void main(void){
        vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
        vec3 color=pixelColor(p);
        gl_FragColor=vec4(color,1.0);
    }
    </script>
    <script id="vs" type="x-shader/x-vertex">
    attribute vec3 position;
    void main(void){
        gl_Position = vec4(position, 1.0);
    }
    </script>
    <meta charset="utf-8">
    <script src="main.js" type="text/javascript"></script>
    <script src="../src/link.js"></script>
    <link rel="stylesheet" href="../src/main.css">
    </head>
    
    <body>
        <canvas id="canvas"></canvas>
    </body>
    
    </html>